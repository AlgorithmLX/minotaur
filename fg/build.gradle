buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}

plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "java"
    id "idea"
}

apply plugin: 'net.minecraftforge.gradle'

version = "1.0.371"
group = "com.modrinth.minotaur"

minecraft {
    mappings channel: "official", version: "1.18.1"
}

dependencies {
    minecraft "net.minecraftforge:forge:1.18.1-39.0.9"
}

idea {
    module {
        downloadJavadoc = false
        downloadSources = false
    }
}

java {
    withSourcesJar()
}

import com.modrinth.minotaur.dependencies.DependencyType
import com.modrinth.minotaur.dependencies.ModDependency
import com.modrinth.minotaur.dependencies.VersionDependency

modrinth {
    apiUrl = STAGING_API_URL
    projectId = "freeeedom"
    uploadFile = jar
    additionalFiles = [project.file("build/libs/fg-$version-sources.jar")]
    versionType = "beta"
    dependencies = [
        new ModDependency("big-mod", "optional"),
        new VersionDependency("IgXqCrMH", DependencyType.INCOMPATIBLE)
    ]
    dependencies {
        required.project "modmod"
        incompatible.version "M8VeghJn"
        embedded.project "updatefixerupper"
    }
    debugMode = true
}

tasks.modrinth.doLast {
    if (!modrinth.debugMode && tasks.modrinth.wasUploadSuccessful()) println tasks.modrinth.uploadInfo.id
}
